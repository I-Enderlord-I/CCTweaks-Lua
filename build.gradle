group 'org.squiddev'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.6

repositories {
    mavenCentral()

    ivy {
        name = "ComputerCraft"
        artifactPattern "http://addons-origin.cursecdn.com/files/2275/878/[module][revision](.[ext])"
    }

    maven {
        name = "squiddev"
        url = "https://dl.bintray.com/squiddev/maven"
    }

	maven {
		name = "forge"
		url = "http://files.minecraftforge.net/maven"
	}
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile "ComputerCraft:ComputerCraft:1.78"
    compile 'com.google.guava:guava:19.0'
    shade "org.ow2.asm:asm-debug-all:5.0.4"

    shade('org.squiddev:luaj.luajc:1.0.5') {
        // We provide a custom LuaJ source with ComputerCraft
        exclude group: 'org.luaj'
    }

    shade 'org.squiddev:Patcher:1.1.1.1'
	shade 'org.squiddev:Cobalt:0.1'

	compile 'org.squiddev:ConfigGen:1.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Build a non-obfuscated jar
task devJar(type: Jar) {
	from sourceSets.main.output

	// Package all the org.squiddev dependencies into one file
	from configurations.shade
		.collect { it.isDirectory() ? it : zipTree(it) }

	// Package the patch files into a patch/ folder
	filesMatching("org/luaj/**", {
		it.path = "patch/" + it.path
	})

	classifier = 'complete'
}

assemble.dependsOn += devJar
